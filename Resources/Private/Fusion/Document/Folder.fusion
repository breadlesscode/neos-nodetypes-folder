prototype(Breadlesscode.NodeTypes.Folder:Document.Folder) < prototype(Neos.Fusion:Case) {
    backend {
        condition = ${documentNode.context.inBackend}
        renderer = Neos.Neos:Page {
            head {
                neosCSS = Neos.Fusion:Tag {
                    @position = 'after stylesheets'
                    tagName = 'link'
                    attributes {
                        rel = 'stylesheet'
                        href = Neos.Fusion:ResourceUri {
                            path = 'resource://Neos.Neos/Public/Styles/Neos.css'
                        }
                    }
                }
                folderCSS = Neos.Fusion:Tag {
                    @position = 'after neosCSS'
                    tagName = 'link'
                    attributes {
                        rel = 'stylesheet'
                        href = Neos.Fusion:ResourceUri {
                            path = 'resource://Breadlesscode.NodeTypes.Folder/Public/Styles/Folder.css'
                        }
                    }
                }
            }
            body {
                templatePath = 'resource://Breadlesscode.NodeTypes.Folder/Private/Templates/Folder.html'
                title = ${q(documentNode).property('title')}
                version = ${Breadlesscode.NodeTypes.Folder.PackageManager.getNeosUiMajorVersion()}
            }
        }
    }

    default {
        condition = true
        renderer = Neos.Fusion:Http.Message {
            httpResponseHead {
                statusCode = 301
                headers {
                    Location = Neos.Neos:NodeUri {
                        node = ${q(documentNode).parent().get(0)}
                    }
                }
            }
        }
    }
}
